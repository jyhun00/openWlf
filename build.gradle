plugins {
    id 'java'
    id 'org.springframework.boot' version '4.0.1' apply false
    id 'io.spring.dependency-management' version '1.1.7'
}

allprojects {
    group = 'aml'
    version = '0.0.1-SNAPSHOT'
    
    repositories {
        mavenCentral()
    }
}

// Exclude root src directory from compilation
sourceSets {
    main {
        java {
            srcDirs = []
        }
    }
    test {
        java {
            srcDirs = []
        }
    }
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'io.spring.dependency-management'
    
    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }
    
    // UTF-8 인코딩 설정
    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
    }
    
    tasks.withType(Javadoc).configureEach {
        options.encoding = 'UTF-8'
    }
    
    tasks.withType(Test).configureEach {
        systemProperty 'file.encoding', 'UTF-8'
    }
    
    dependencyManagement {
        imports {
            mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
        }
    }
    
    dependencies {
        implementation 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    }

    tasks.named('test') {
        useJUnitPlatform()
    }
}
